$!
$!	BUILDREAD.COM, a procedure to concatenate the FREEWARE_README.TXT's
$!		with form feeds separating the files.
$!
$	ON ERROR THEN $GOTO ERREXIT
$! Edit the next line for later freeware versions
$ P1="V3"
$	DIRE/NOHEAD/NOTRAIL/OUTPUT=FILENAMES.TMP [*...]FREEWARE_README.TXT
$	SET MESS/NOFAC/NOID/NOTEXT/NOSEVER
$	CREATE FORMFEED.MEM
.PAGE
.BLANK 2
$	RUNOFF FORMFEED.MEM
$	COPY FORMFEED.MEM AAAREADME.'P1'
$	OPEN/READ FILENAMES FILENAMES.TMP
$	READ FILENAMES FILENAME
$	OPEN/WRITE TEMP AAAREADME.TMP
$	FILENAME = FILENAME - F$LOGICAL("SYS$DISK")
$	WRITE TEMP "========> ''FILENAME' <========"
$	CLOSE TEMP
$ 	APPEND AAAREADME.TMP AAAREADME.'P1'
$	IF F$FILE(FILENAME,"RAT") .EQS. "" THEN GOTO FILE_OK
$	CREATE AAAREADME.TMP
.PAGE
.BLANK 2
.LITERAL
$	APPEND 'FILENAME' AAAREADME.TMP
$	APPEND SYS$INPUT AAAREADME.TMP
.END LITERAL
$	RUNOFF/OUTPUT=AAAREADME.TMR AAAREADME.TMP
$	APPEND AAAREADME.TMR AAAREADME.'P1'
$	DELETE AAAREADME.TMP;0
$	DELETE AAAREADME.TMR;0
$	GOTO LOOP
$FILE_OK:
$	APPEND 'FILENAME' AAAREADME.'P1';0
$LOOP:
$	READ/END_OF_FILE=DONE FILENAMES FILENAME
$	OPEN/WRITE TEMP AAAREADME.TMP
$	FILENAME = FILENAME - F$LOGICAL("SYS$DISK")
$	WRITE TEMP "========> ''FILENAME' <========"
$	WRITE SYS$OUTPUT "========> ''FILENAME' <========"
$	CLOSE TEMP
$	APPEND FORMFEED.MEM AAAREADME.'P1'
$ 	APPEND AAAREADME.TMP AAAREADME.'P1'
$	IF F$FILE(FILENAME,"RAT") .EQS. "" THEN GOTO FILE_APPEND
$	CREATE AAAREADME.TMP
.LITERAL
$	APPEND 'FILENAME' AAAREADME.TMP
$	APPEND SYS$INPUT AAAREADME.TMP
.END LITERAL
$	RUNOFF/OUTPUT=AAAREADME.TMR AAAREADME.TMP
$	APPEND AAAREADME.TMR AAAREADME.'P1'
$	DELETE AAAREADME.TMP;*
$	DELETE AAAREADME.TMR;*
$	GOTO LOOP
$FILE_APPEND:
$	APPEND 'FILENAME' AAAREADME.'P1'
$	GOTO LOOP
$ERREXIT:
$	SET MESS/FAC/ID/TEXT/SEVER
$	WRITE SYS$OUTPUT "***BUILDREAD.COM FAILED***"
$DONE:
$	SET MESS/FAC/ID/TEXT/SEVER
$	CLOSE FILENAMES
$	DELETE FILENAMES.TMP.*
$	DELETE FORMFEED.MEM.*
$	WRITE SYS$OUTPUT "Output is in AAAREADME.''P1'
$!
$!	END OF BUILDREAD.COM
$!
