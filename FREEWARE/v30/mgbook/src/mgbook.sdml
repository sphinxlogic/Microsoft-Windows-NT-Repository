<COMMENT>

 File :		MGBOOK.SDML
 Author :	Darrell Burkhead
 Date :		July 18, 1995

 Description:

	This file is used to generate the on-line help for MGBOOK. 
	The following command create the help file.

		$ DOCUMENT MGBOOK HELP HLP		! Creates MGBOOK.HLP

	You can then add the help to a help library with:

		$ LIBRARY/REPLACE/HELP SYS$HELP:HELPLIB MGBOOK

	If you want to use an alternate help library than HELPLIB, you
	should replace the "SYS$HELP:HELPLIB" in the command above with
	the location and name of the alternate help library.

 Revision History:

	01-000		Darrell Burkhead	18-JUL-1995 17:47
		Creation.

<ENDCOMMENT>

<COMMAND_SECTION>(MGBOOK Command\MGBOOK)
<COMMAND>(MGBOOK\\MGBOOK_CMD)
<OVERVIEW>
MGBOOK is a character-cell based interface to the BOOKREADER
documentation, i.e., it allows you to read .DECW$BOOK and
.DECW$BOOKSHELF files on a VT-series terminal.
<ENDOVERVIEW>

<FORMAT>
<FCMD>(MGBOOK) <FPARMS>([/qualifiers] [filename])
<QUAL_LIST>
<QPAIR>(/BOOK\)
<QPAIR>(/DEBUG\/NODEBUG)
<QPAIR>(/RESTRICT_WIDTH\/NORESTRICT_WIDTH)
<QPAIR>(/SHELF\/SHELF)
<QPAIR>(/TAB\)
<ENDQUAL_LIST>
<ENDFORMAT>

<PARAMDEFLIST>
<PARAMITEM>(filename)
<PARAMDEF>
Specifies the name of a book or shelf file to open.  The /BOOK
qualifier should be used if a book file is specified.  If this
parameter is omitted, the default library files are opened.  The
default types for book and shelf files are .DECW$BOOK and
.DECW$BOOKSHELF respectively.
<ENDPARAMDEFLIST>

<QUALDEFLIST>
<QUALITEM>(/BOOK)
<QUALDEF>
Specifies that the initial file to open is a book (.DECW$BOOK) file. 
This qualifier should not be used without the filename parameter.
<QUALITEM>(/DEBUG\/NODEBUG (D))
<QUALDEF>
Enables debugging output when reading book files. A name.LOG file,
where "name" is the name of the book file, will be created in your
login directory whenever a book file is read.  There is no official
debugging output format.  It may change between each version of
MGBOOK.  It is modified to suit the needs of the author at the time. 
Thus, /DEBUG isn't all that useful to the average MGBOOK user.
<QUALITEM>(/RESTRICT_WIDTH\/NORESTRICT_WIDTH (D))
<QUALDEF>
Controls whether MGBOOK will try to resize the screen to fit book or
menu text.  If /RESTRICT_WIDTH is used, MGBOOK will not resize the
screen.  This option can be toggled within MGBOOK with <KEY>(KP9).
<QUALITEM>(/SHELF\/SHELF (D))
<QUALDEF>
Specifies that the initial file to open is a shelf (.DECW$BOOKSHELF)
file.
<QUALITEM>(/TAB)
<QUALDEF>
Used to temporarily switch the terminal Tab setting.  If /TAB is
specified, the terminal will be set to Tab; if /NOTAB is specified,
the terminal will be set to NoTab.  This qualifier is useful if the
tab settings on the terminal are not set to one every 8 characters. 
Temporarily disabling Tab will insure that book or shelf text is not
misdrawn when MGBOOK tries to optimize by using tabs.
<ENDQUALDEFLIST>

<EXAMPLE_SEQUENCE>
<EXI><S>($ )<U>(MGBOOK)
<EXTEXT>
The basic MGBOOK command will open the default library file(s)
specified by either MGBOOK_LIBRARY, DECW$BOOKSHELF, or DECW$BOOK.  See
Logical_Names for more information.
<EXI><S>($ )<U>(MGBOOK/BOOK SYS$LOGIN:MYBOOK)
<EXTEXT>
In this example, MGBOOK will initially open the book file
SYS$LOGIN:MGBOOK.DECW$BOOKSHELF.  The default directory for book files
is DECW$BOOK:, so you should include the device and directory name if
your book file is not located within DECW$BOOK:.
<EXI><S>($ )<U>(MGBOOK SYS$LOGIN:MYSHELF)
<EXTEXT>
In this example, MGBOOK will initially open the shelf file
SYS$LOGIN:MYSHELF.DECW$BOOKSHELF.
<ENDEXAMPLE_SEQUENCE>

<DESCRIPTION>(Logical_Names)
MGBOOK uses several logical names to determine the location of book
and shelf files.  Initially, if a filename is not provided on the
command line, MGBOOK will search for:

<LIST>(UNNUMBERED)
<LE>MGBOOK_LIBRARY:*.DECW$BOOKSHELF, if MGBOOK_LIBRARY is defined, or
<LE>DECW$BOOKSHELF:*.DECW$BOOKSHELF, if DECW$BOOKSHELF is defined, or
<LE>DECW$BOOK:LIBRARY.DECW$BOOKSHELF, otherwise
<ENDLIST>

MGBOOK builds a shelf menu out of the files that match one of the
criteria above (or displays the shelf found if only one shelf file is
found).  From there, when a book or shelf file is selected MGBOOK searches
for the matching filename in either the same directory as the shelf
file or DECW$BOOK:.
<ENDDESCRIPTION>

<DESCRIPTION>(Keyboard_Commands)
The following keys will work anywhere within MGBOOK:

<TABLE>(Universal Keys)
<TABLE_SETUP>(2\20)
<TABLE_HEADS>(Key\Function)
<TABLE_ROW>(Control-D\Spawn a subprocess.)
<TABLE_ROW>(Control-E\Exit MGBOOK.)
<TABLE_ROW>(Control-R\Refresh the screen.)
<TABLE_ROW>(Control-W\Same as Control-R.)
<TABLE_ROW>(Control-Z\Close the current display.)
<TABLE_ROW>(F10\Close the current display.)
<TABLE_ROW>(KP0\Display the status box.)
<TABLE_ROW>(KP9\Toggle the /RESTRICT_WIDTH setting.)
<TABLE_ROW>(?\Display help for the keys currently available.)
<TABLE_ROW>(Help\Same as ?.)
<ENDTABLE>

The following keys are available within a shelf menu:

<TABLE>(Shelf Keys)
<TABLE_SETUP>(2\20)
<TABLE_HEADS>(Key\Function)
<TABLE_ROW>(Control-U\Clear the search string.)
<TABLE_ROW>(Delete\Delete a character from the search string.)
<TABLE_ROW>(Do\Select the current shelf item.)
<TABLE_ROW>(Down Arrow\Scroll down by one menu item.)
<TABLE_ROW>(Enter\Same as Do.)
<TABLE_ROW>(F1\Create a new private shelf file 
(SYS$LOGIN:LIBRARY.DECW$$BOOKSHELF).)
<TABLE_ROW>(F2\Append to or create a private shelf file.)
<TABLE_ROW>(F3\Close the private shelf file.)
<TABLE_ROW>(F4\Write the current line to the private shelf file.)
<TABLE_ROW>(Find\Find the next occurence of the search string.)
<TABLE_ROW>(KP1\Same as F1.)
<TABLE_ROW>(KP2\Same as F2.)
<TABLE_ROW>(KP3\Same as F3.)
<TABLE_ROW>(KP4\Same as F4.)
<TABLE_ROW>(KP5\Scroll up by one menu item.)
<TABLE_ROW>(KP6\Same as Down Arrow.)
<TABLE_ROW>(KP7\Scroll up by one screen.)
<TABLE_ROW>(KP8\Scroll down by one screen.)
<TABLE_ROW>(Left Arrow\Close the current shelf.)
<TABLE_ROW>(Next Screen\Same as KP7.)
<TABLE_ROW>(PF3\Same as Find.)
<TABLE_ROW>(Previous Screen\Same as KP8.)
<TABLE_ROW>(Return\Same as Do.)
<TABLE_ROW>(Right Arrow\Same as Do.)
<TABLE_ROW>(Select\Same as Do.)
<TABLE_ROW>(Up Arrow\Same as KP5.)
<TABLE_ROW>(Printable Characters\Add that character to the search string.)
<ENDTABLE>

The following keys are available within a book menu:

<TABLE>(Book-Menu Keys)
<TABLE_SETUP>(2\20)
<TABLE_HEADS>(Key\Function)
<TABLE_ROW>(Control-U\Clear the search string.)
<TABLE_ROW>(Delete\Delete a character from the search string.)
<TABLE_ROW>(Do\Select the current shelf item.)
<TABLE_ROW>(Down Arrow\Scroll down to the next lower menu item at the same
or greater level in the hierarchy.)
<TABLE_ROW>(Enter\Same as Do.)
<TABLE_ROW>(Find\Find the next occurence of the search string.)
<TABLE_ROW>(KP5\Scroll up by one menu item.)
<TABLE_ROW>(KP6\Same as Down Arrow.)
<TABLE_ROW>(KP7\Scroll up by one screen.)
<TABLE_ROW>(KP8\Scroll down by one screen.)
<TABLE_ROW>(Left Arrow\Move up one level in the hierarchy or close the current
menu.)
<TABLE_ROW>(Next Screen\Same as KP7.)
<TABLE_ROW>(PF3\Same as Find.)
<TABLE_ROW>(Previous Screen\Same as KP8.)
<TABLE_ROW>(Return\Same as Do.)
<TABLE_ROW>(Right Arrow\Move down one level in the hierarchy or select the
current item.)
<TABLE_ROW>(Select\Same as Do.)
<TABLE_ROW>(Up Arrow\Scroll up to the next higher menu item at the same or
greater level in the hierarchy.)
<TABLE_ROW>(Printable Characters\Add that character to the search string.)
<ENDTABLE>

The following keys are available while reading book text:

<TABLE>(Book-Text Keys)
<TABLE_SETUP>(2\20)
<TABLE_HEADS>(Key\Function)
<TABLE_ROW>(Control-B\Go back to the last section selected.)
<TABLE_ROW>(Down Arrow\Scroll down by one line.)
<TABLE_ROW>(Find\Find the next hotspot.)
<TABLE_ROW>(KP5\Scroll up by one line.)
<TABLE_ROW>(KP6\Same as Down Arrow.)
<TABLE_ROW>(KP7\Scroll up by one screen.)
<TABLE_ROW>(KP8\Scroll down by one screen.)
<TABLE_ROW>(KP Period\Use the current hotspot.)
<TABLE_ROW>(Left Arrow\Move to the previous section.)
<TABLE_ROW>(Next Screen\Same as KP8.)
<TABLE_ROW>(Previous Screen\Same as KP7.)
<TABLE_ROW>(Right Arrow\Move to then next section.)
<TABLE_ROW>(Select\Same as KP Period.)
<ENDTABLE>
<ENDDESCRIPTION>

<DESCRIPTION>(Author)
MGBOOK is a MadGoat Software product written by Darrell Burkhead.
<P>
Copyright © 1995, MadGoat Software.  All rights reserved.
<ENDCOMMAND_SECTION>
