1       !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
        ! Program: INT_DEMO_F_WINDOW
        ! System : INTOUCH DEMONSTRATION SYSTEM
        ! Author : Allen
        ! Date   : May 2, 1991
        ! Purpose: illustrate "windowing" capability
        !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

        frame off

        z1$ = space$(40)
        cset z1$ = "Creating Windows"
        print wide, reverse, at 1, 1 : z1$;

        print reverse, at 24, 1 : space$(80);

        clear area box : 3, 38, 13, 76
        for z = 4 to 12 \ print at z, 39 : repeat$("1", 37) \ next z
        print       at  6, 50 : "               ";
        print bold, at  7, 50 : "  W I N D O W  ";
        print       at  8, 50 : "               ";
        print bold, at  9, 50 : "     O N E     ";
        print       at 10, 50 : "               ";
        ask window : current first$

        z1$ = "Press the RETURN key to see WINDOW TWO"
        gosub pause

        clear area box : 6, 5, 16, 43
        for z = 7 to 15 \ print bold, at z, 6 : repeat$("2", 37) \ next z
        print at  9, 17 : "               ";
        print at 10, 17 : "  W I N D O W  ";
        print at 11, 17 : "               ";
        print at 12, 17 : "     T W O     ";
        print at 13, 17 : "               ";
        ask window : current second$

        z1$ = "Press the RETURN key to see WINDOW THREE"
        gosub pause

        clear area box : 10, 11, 20, 70
        for z = 11 to 19 \ print reverse, at z, 12 : repeat$("3", 58) \ next z
        print       at 13, 32 : "               ";
        print bold, at 14, 32 : "  W I N D O W  ";
        print       at 15, 32 : "               ";
        print bold, at 16, 32 : "   T H R E E   ";
        print       at 17, 32 : "               ";

        z1$ = "Press the RETURN key to erase WINDOW THREE"
        gosub pause

        set window : current second$

        z1$ = "Press the RETURN key to erase WINDOW TWO"
        gosub pause

        set window : current first$

        delay

        stop

        routine pause
          z = 40 - (len(z1$) / 2)
          print bold, at 23, z : z1$;

          do
            key input z$
          loop until _terminator = "RETURN"
        end routine

        end
