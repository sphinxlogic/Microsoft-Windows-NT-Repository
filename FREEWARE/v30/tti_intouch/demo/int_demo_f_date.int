1       !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
        ! Program: INT_DEMO_F_DATE
        ! System : INTOUCH DEMONSTRATION SYSTEM
        ! Author : Allen
        ! Date   : May 16, 1991
        ! Purpose: illustrate DATE$() facilities.
        !%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

        clear area box : 7, 21, 18, 58

        print at  8, 23 : "Julian date   : ";

        print at 10, 23 : "YYYYMMDD      : ";
        print at 11, 23 : "MMDDYYYY      : ";
        print at 12, 23 : "DDMMYYYY      : ";

        print at 14, 23 : "DD-Mon-YYYY   : ";
        print at 15, 23 : "Month DD, YYYY: ";

        print at 17, 23 : "Day of week   : ";

        today_yyyymmdd$ = date$
        today_mmddyyyy$ = date$(days(today_yyyymmdd$), 1)

        do
          line input at 4, 1, &
               prompt "Date (MMDDYYYY)? ", &
               length 8, &
               valid "DATE:MDCY", &
               default today_mmddyyyy$ : mmddyyyy$

          if  _back  or  _exit  then  exit do

          jul = days(mmddyyyy$[5:8] + mmddyyyy$[1:4])

          print bold, at  8, 39 : rpad$(str$(jul), 9);

          print bold, at 10, 39 : date$(jul, 0);
          print bold, at 11, 39 : date$(jul, 1);
          print bold, at 12, 39 : date$(jul, 2);

          print bold, at 14, 39 : date$(jul, 3);
          print bold, at 15, 39 : rpad$(date$(jul, 4), 18);

          print bold, at 17, 39 : rpad$(day$(jul), 9);

          delay          
        loop

        end
