$!
$!	Installing Python for VMS:
$!
$!	Source: http://vmspython.dyndns.org/
$!
$!     Assume files PYTHON-2_3-VMS-SRC.ZIP is present
$!     Need libz and libbz2 to be installed
$!
$!     P1: root directory where to install Python
$!
$
$ if p1 .eqs. "" then $ exit 229452
$
$ if f$type(unzip) .eqs. ""
$ then
$   write sys$output "unzip not found"
$   exit 44
$ endif
$ if f$trnlnm("libbz2") .eqs. ""
$ then
$   write sys$output "logical libbz2 not found"
$   exit 44
$ endif
$ if f$trnlnm("libbz2_shr32") .eqs. ""
$ then
$   write sys$output "logical libbz2_shr32 not found"
$   exit 44
$ endif
$ if f$trnlnm("libz") .eqs. ""
$ then
$   write sys$output "logical libz not found"
$   exit 44
$ endif
$ if f$trnlnm("libz_shr32") .eqs. ""
$ then
$   write sys$output "logical libz_shr32 not found"
$   exit 44
$ endif
$
$ create/dir 'p1'
$ 
$ root_dev = f$parse(p1,,,"DEVICE","NO_CONCEAL")
$ root_dir = f$parse(p1,,,"DIRECTORY","NO_CONCEAL")
$ root_dir = f$extract(1,f$length(root_dir)-2,root_dir)
$ root_dir = root_dir - "["
$ root_dir = root_dir - "]"
$ root_dir = root_dir - "<"
$ root_dir = root_dir - ">"
$ root_dir = root_dir - ".000000"
$ root_dir = root_dir - "000000"
$
$ pyhome := 'root_dev'['root_dir']
$ define/trans=(conc,term) pyhome 'root_dev'['root_dir'.]
$
$ sh log pyhome
$
$! SET VERIFY
$ SET NOON
$
$ unzip -j PYTHON-2_3-VMS-SRC.ZIP
$
$!  Next cmd: ignore ODS-5 file errors (ie files containing ^ that fail)
$ dest = pyhome - "]" + "...]"
$ backup python-2_3-vms-src.bck/sav/sel=[python-2_3...]  'dest'/new_version
$ delete python-2_3-vms-src.bck;
$
$ set default PYHOME:[VMS]
$
$ @logicals
$
$ @build_all
$
$ exit
