<!-- LANGUAGE_015.HTML continuation of DOCDATA:[DOC.INTOUCH]LANGUAGE.HTML -->
<html>
<head>
<title>INTOUCH&#174; 4GL </title>
</head>
<body background="/white.gif">
<h1 align="center">INTOUCH<sup>&#174;</sup> 4GL <br> A Guide to the INTOUCH Language</h1>

<hr> <a href="LANGUAGE_014.HTML#bottom_014">Previous page...</a> 
<a href="LANGUAGE_CONTENTS.HTML">Table of Contents</a>

<hr size=5>

<a name="heading_14.7.4"><h2>14.7.4 FOR EACH/NEXT</h2></a>
<a name="index_x_904"></a>
<a name="index_x_905"></a>
<a name="index_x_906"></a>
<h2>FORMAT:</h2>

<p><pre>
        FOR EACH struc_name 
                ---      
                ---  block of code 
                --- 
        NEXT struc_name 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client' 
        20  EXTRACT STRUCTURE cl 
              INCLUDE cl(state) = 'CA' 
              EXCLUDE cl(phone)[1:3] = '619' 
              SORT ASCENDING BY cl(last) 
            END EXTRACT 
            PRINT 'List of California Clients' 
            PRINT 
        30  FOR EACH cl 
              PRINT cl(first); ' '; cl(last), cl(phone) 
            NEXT cl 
        40  CLOSE STRUCTURE cl 
        50 END 
 
        RNH 
        List of California Clients 
 
        Dale Derringer      (818) 223-9014 
        Earl Errant         (408) 844-7676 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
You can use the <b>FOR EACH</b> statement to execute a block of 
code for each record in the extract list.  This allows you to manipulate 
structure information in your programs. 

<p>
<a name="index_x_907"></a>
The FOR EACH statement begins a loop that executes a block of code for each 
record in the extract list.  <i>struc_name</i> is the structure name 
associated with the structure.  <b>NEXT struc_name</b> marks 
the end of the loop. 

<p>
<a name="index_x_908"></a>
<a name="index_x_909"></a>
<a name="index_x_910"></a>
<a name="index_x_911"></a>
<a name="index_x_912"></a>
<a name="index_x_913"></a>
You can use the <b>[REPEAT]</b>, <b>[ITERATE]</b> and 
<b>EXIT FOR</b> statements in the FOR EACH loop. 
</dl>

<p>

<a name="heading_14.7.5"><h2>14.7.5 EXTRACT STRUCTURE: KEY</h2></a>
<a name="index_x_914"></a>
<a name="index_x_915"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXTRACT STRUCTURE struc_name: KEY field = expr1 [TO expr2] 
                  --- 
                  ---  block of code 
                  --- 
        END EXTRACT 
</pre>

<dl>
<dd>

<p>
<i>struc_name</i> is the structure name associated with the structure. 
<i>field</i> is the name of the field that contains the key.  
<i>expr</i> is an expression that tells what key(s) to extract. 
INTOUCH evaluates the expression, checks the structure's index for records 
with matching keys, and extracts these records (if any records are found). 
</dl>

<p>
<h2>KEY Option</h2>

<dl>
<dd>

<p>
The <b>KEY</b> option includes records using the record's 
<i>key</i>.  The key is a field which has an index for fast access. 
The key option can considerably speed up extractions.  

<p>
The conditional expression must match the field's data type.  For 
instance, if the field has a CHARACTER data type, the expression 
must be a string expression.  

<p>
For example, we have a structure with the following client information 
and the ID field is a key field: 

<p><pre>
        ID #     LAST       FIRST       CITY     STATE  PHONE 
      +------+-----------+--------+--------------+--+----------+ 
      |80543 |Cass       |Cathy   | San Diego    |CA|6197438582| 
      |80542 |Brock      |Bud     | Duluth       |MN|2185554322| 
      |80522 |Errant     |Earl    | Monterey     |CA|4088447676| 
      |80561 |Derringer  |Dale    | Los Angeles  |CA|8182239014| 
      |80531 |Abott      |Al      | New York     |NY|2025669892| 
      |80573 |Farmer     |Fred    | Miami        |FL|3055527872| 
</pre>

<p>
In the program below, the KEY option is used to extract the client with 
the ID number 80561. 
</dl>

<p>
<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'TTI_RUN:CLIENT' 
        20  EXTRACT STRUCTURE cl: KEY id = 80561 
              PRINT 'Client:', 
              PRINT cl(first); ' '; cl(last), cl(id) 
            END EXTRACT 
            CLOSE STRUCTURE cl 
        30  END 
 
        RNH 
        Client:            Dale Derringer        80561 
</pre>

<p>
<h2>TO expr Option</h2>

<a name="index_x_916"></a>
<a name="index_x_917"></a>
<dl>
<dd>

<p>
You can extract records with keys in a certain range with the 
<b>TO</b> option.  <i>expr1</i> is the lowest key to check. 
<i>expr2</i> is the highest.  INTOUCH extracts any records whose keys 
are within the range specified. 
</dl>

<p>
<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'TTI_RUN:CLIENT' 
        20  INPUT 'Enter the lowest ID to check': lowest 
            INPUT 'Enter the highest ID to check': highest 
            EXTRACT STRUCTURE cl: KEY id = lowest TO highest 
              PRINT cl(id); TAB(10); cl(last) 
            END EXTRACT 
            CLOSE STRUCTURE cl 
        30  END 
 
        RNH 
        Enter the lowest ID to check? 80540 
        Enter the highest ID to check? 80570 
        80542    Brock 
        80543    Cass 
        80561    Derringer 
</pre>

<a name="heading_14.7.6"><h2>14.7.6 EXTRACT STRUCTURE, FIELD: PARTIAL KEY</h2></a>
<a name="index_x_918"></a>
<a name="index_x_919"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXTRACT STRUCTURE struc_name, FIELD field_expr: PARTIAL KEY str_expr 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'TTI_RUN:CLIENT' 
            EXTRACT STRUCTURE cl, FIELD last: PARTIAL KEY 'Ros' 
            END EXTRACT 
            PRINT 'List of clients with last name starting with Ros' 
            PRINT 
            FOR EACH cl 
              PRINT cl(first); ' '; cl(last) 
            NEXT cl 
        20  CLOSE STRUCTURE cl 
        30  END 
 
        RNH 
        Bud Roske 
        Earl Rost 
        Dale Rosty 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
The <b>PARTIAL KEY</b> option will search in the EXTRACT STRUCTURE 
for part of a key value. 

<p>
Here is a structure with the following client information and the ID is a 
key field: 

<p><pre>
         ID #      LAST      FIRST      CITY      STATE  PHONE 
      +------+-----------+--------+--------------+--+----------+ 
      |80543 |Roberts    |Cathy   | San Diego    |CA|6197438582| 
      |80542 |Roske      |Bud     | Duluth       |MN|2185554322| 
      |80522 |Rost       |Earl    | Monterey     |CA|4088447676| 
      |80561 |Rosty      |Dale    | Los Angeles  |CA|8182239014| 
      |80531 |Abott      |Al      | New York     |NY|2025669892| 
      |80573 |Farmer     |Fred    | Miami        |FL|3055527872| 
</pre>

<p>
The above example program creates an extract list containing only those 
clients with a last name starting with "ROS".  
</dl>

<p>

<a name="heading_14.7.7"><h2>14.7.7 CANCEL EXTRACT</h2></a>
<a name="index_x_920"></a>
<a name="index_x_921"></a>
<a name="index_x_922"></a>
<h2>FORMAT:</h2>

<p><pre>
        CANCEL EXTRACT 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'TTI_RUN:CLIENT' 
            EXTRACT STRUCTURE cl 
              PRINT 'Client: '; cl(last) 
              LINE INPUT 'Press return to continue': z$ 
              IF  _EXIT  THEN  CANCEL EXTRACT 
            END EXTRACT 
            PRINT 'Records extracted:'; _EXTRACTED 
        20  CLOSE STRUCTURE cl 
        30  END 
 
        RNH 
        Client: Smith 
        Press return to continue? EXIT 
        Records extracted: 0 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
<b>CANCEL EXTRACT</b> cancels the current extract of a record 
and transfers control to the next statement after the END EXTRACT statement. 

<p>
This statement can <i>only</i> be used within an EXTRACT block---that 
is, between an EXTRACT STRUCTURE and an END EXTRACT pair of statements. 
</dl>

<p>

<a name="heading_14.7.8"><h2>14.7.8 EXIT EXTRACT</h2></a>
<a name="index_x_923"></a>
<a name="index_x_924"></a>
<a name="index_x_925"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXIT EXTRACT 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client' 
            EXTRACT STRUCTURE cl 
              PRINT 'Client: '; cl(last) 
              LINE INPUT 'Press return to continue': z$ 
              IF  _EXIT  THEN  EXIT EXTRACT 
            END EXTRACT 
            PRINT 'Records extracted:'; _EXTRACTED 
        20  END 
 
        RNH 
        Client: Smith 
        Press return to continue? &lt;RETURN&gt; 
        Client: Kent 
        Press return to continue? EXIT 
        Records extracted: 1 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
<b>EXIT EXTRACT</b> passes control to the corresponding 
END EXTRACT statement, performs final sorting (if any), and creates 
the extracted collection. 
</dl>

<p>

<a name="heading_14.7.9"><h2>14.7.9 REEXTRACT STRUCTURE</h2></a>
<a name="index_x_926"></a>
<a name="index_x_927"></a>
<a name="index_x_928"></a>
<h2>FORMAT:</h2>

<p><pre>
        REEXTRACT STRUCTURE struc_name 
                  --- 
           [INCLUDE | EXCLUDE] cond_expr... 
           [SORT [ASCENDING | DESCENDING] BY expression... 
                  --- 
        END EXTRACT 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client', ACCESS INPUT 
        20  EXTRACT STRUCTURE cl 
              INCLUDE cl(state) = 'CA' 
            END EXTRACT 
            REEXTRACT STRUCTURE cl 
              EXCLUDE cl(phone)[1:3] &lt;&gt; '619' 
              SORT ASCENDING BY cl(last) 
            END EXTRACT 
        30  PRINT 'List of California Clients in Area Code 619' 
            FOR EACH cl 
              PRINT cl(first); ' '; cl(last), cl(phone) 
            NEXT cl 
        40  CLOSE STRUCTURE cl 
        50  END 
 
        RNH 
        List of California Clients in Area Code 619 
        Cathy Cass          (619) 743-8582 
        Paul Johnson        (619) 489-5551 
        Keith Kent          (619) 967-5021 
        Pete Porter         (619) 778-6709 
        Wayne Waters        (619) 564-1231 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
You can use <b>REEXTRACT STRUCTURE</b> to do a second extract on 
a list of structure records you previously extracted.  This lets you choose 
more and more specific records through a series of REEXTRACTs. 

<p>
REEXTRACT does an extract on the list of records previously extracted.  
<i>struc_name</i> is the structure name associated with an open 
structure.  

<p>
<b>END EXTRACT</b> marks the end of the REEXTRACT construct. 
REEXTRACT operates the same as EXTRACT.  However, REEXTRACT operates on a 
previously extracted list. 
</dl>

<hr>
<blockquote><strong><p align=center>Note</p>
Extract operations by key cannot be performed with REEXTRACT. 
</strong></blockquote><hr>

<p>

<a name="heading_14.7.10"><h2>14.7.10 EXTRACT STRUCTURE: APPEND</h2></a>
<a name="index_x_929"></a>
<a name="index_x_930"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXTRACT STRUCTURE struc_name: APPEND 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE detail: name 'tti_run:detail' 
            SET STRUCTURE detail: EXTRACTED 0 
        20  EXTRACT STRUCTURE detail, FIELD lineid : &amp; 
                KEY '10301001' TO '10301999', APPEND 
              SORT BY detail(prodnbr) 
              SORT BY detail(invnbr) 
            END EXTRACT 
        30  EXTRACT STRUCTURE detail, field lineid : &amp; 
                KEY '10311001' to '10311999', APPEND 
              SORT BY detail(prodnbr) 
              SORT BY detail(invnbr) 
            END EXTRACT 
            PRINT 'Prod'; TAB(7); 'Line ID'; TAB(17); 'Qty' 
        40  FOR EACH detail 
              PRINT detail(prodnbr); TAB(7); detail(lineid); &amp; 
                    TAB(17); detail(qty) 
            NEXT detail 
        50  END 
 
        RNH 
        Prod  Line ID   Qty 
        22800 10301-002      2 
        22800 10301-004      1 
        22800 10301-006      2 
        24100 10311-003      1 
        24200 10301-003      1 
        24200 10311-009      1 
        28400 10311-001      2 
        28800 10301-009      2 
        28800 10311-002      9 
        28800 10311-005      1 
        28800 10311-006      1 
        31020 10301-005      1 
        31040 10311-010      2 
        31150 10301-001      1 
        31150 10301-008      8 
        31150 10311-004      1 
        31150 10311-008      1 
        33090 10301-007      2 
        33090 10311-007      1 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
The <b>EXTRACT STRUCTURE: APPEND</b> statement adds records to 
the last collection of extracted records rather than creating a new 
collection. 
</dl>

<p>

<a name="heading_14.7.11"><h2>14.7.11 EXTRACT STRUCTURE, SET, USING</h2></a>
<a name="index_x_931"></a>
<a name="index_x_932"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXTRACT STRUCTURE struc_name1, SET 'set_name', USING struc_name2 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE class: name 'devint:intest_dbms' 
        20  OPEN STRUCTURE part : name 'devint:intest_dbms' 
            EXTRACT STRUCTURE class, SET 'class_part', USING part 
            END EXTRACT 
        30  END 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
<a name="index_x_933"></a>
Used for DBMS handling, this statement fetches all structures, 
(<i>struc_name1</i>) owned by another structure 
(<i>struct_name2</i>) within a given set. 
</dl>

<p>

<a name="heading_14.7.12"><h2>14.7.12 EXTRACT STRUCTURE, SET, FIELD: KEY</h2></a>
<a name="index_x_934"></a>
<a name="index_x_935"></a>
<h2>FORMAT:</h2>

<p><pre>
        EXTRACT STRUCTURE struc_name1, SET 'set_name', FIELD field_expr: KEY str_expr 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE class:  name 'devint:intest_dbms' 
        20  OPEN STRUCTURE part :  name 'devint:intest_dbms' 
            EXTRACT STRUCTURE class, SET 'class_part', FIELD class_code: KEY cl_code$ 
            END EXTRACT 
        30  END 
</pre>

<h2>DESCRIPTION:</h2>

<a name="index_x_936"></a>
<dl>
<dd>

<p>
Used in DBMS handling, this statement allows you to extract all structures 
specified by <i>struc_name1</i> owned by the current record within the 
given set (<i>set_name</i>) with field <i>field_expr</i> equal 
to the given key. 
</dl>

<p>

<a name="heading_14.8"><h1>14.8 ASK STRUCTURE</h1></a>
<a name="index_x_937"></a>
<a name="index_x_938"></a>
<a name="index_x_939"></a>

<p>
The <b>ASK STRUCTURE</b> statement is used to ask about various 
device and structure characteristics from within your programs. 

<p>
<h2>FORMAT:</h2>

<p><pre>
        ASK STRUCTURE struc_name: struc_option [num_var | str_var] 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
<i>struc_name</i> is the name of a structure whose characteristics are 
being asked about.  <i>struc_option</i> can be any of the structure 
options available.  The options are explained in the following sections. 
</dl>

<p>

<a name="heading_14.8.1"><h2>14.8.1 ASK STRUCTURE FIELD: item</h2></a>
<a name="index_x_940"></a>
<h2>FORMAT:</h2>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: item var 
</pre>

<h2>EXAMPLE:</h2>

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client' 
        20  ASK STRUCTURE cl: FIELDS num_fields 
            FOR i = 1 TO num_fields 
              CLEAR 
              ASK STRUCTURE cl, FIELD #i: DESCRIPTION b$ 
              ASK STRUCTURE cl, FIELD #i: PROMPT a$ 
              ASK STRUCTURE cl, FIELD #i: POSITION a% 
              ASK STRUCTURE cl, FIELD #i: LENGTH b% 
              ASK STRUCTURE cl, FIELD #i: HEADING f$ 
              ASK STRUCTURE cl, FIELD #i: PRINTMASK c$, &amp; 
                                          SCREENMASK d$, &amp; 
                                          HELP e$ 
              PRINT AT 5,5: '' 
              PRINT 'Description   : '; b$ 
              PRINT 'Prompt        : '; a$ 
              PRINT 'Position      : '; a% 
              PRINT 'Field length  :' ; b% 
              PRINT 'Rpt. heading  : '; f$ 
              PRINT 'Print mask    : '; c$ 
              PRINT 'Screen mask   : '; d$ 
              PRINT 'Help          : '; e$ 
              DELAY 5 
            NEXT i 
            CLOSE STRUCTURE cl 
        30  END 
 
        RNH 
        Description   : Client ID number 
        Prompt        : Client ID number 
        Position      :  1 
        Field length  : 5 
        Rpt. heading  : CLNT,ID 
        Print mask    : &gt;#### 
        Screen mask   : digits:##### 
        Help          : Client ID number 
</pre>

<h2>DESCRIPTION:</h2>

<dl>
<dd>

<p>
The <b>FIELD</b> option allows you to get information about a 
specific <i>field</i> in a structure.  <i>struc_name</i> is 
the name of the structure.  <i>field_expr</i> is the field you are 
inquiring about.  <i>item</i> specifies what type of information 
you are asking.  The information is stored in the variable specified.  

<p>
The following sections provide more information on what you can use for 
<i>field_expr</i> and on the various field <i>items</i>. 
</dl>

<p>

<hr>
<blockquote><strong><p align=center>Note</p>
The <i>ITEM</i> information is created when the the SETUP routine is 
used to define the field.  You can refer to Chapter 16, Creating Structures, Field Definitions with SETUP for 
information on defining fields. 
</strong></blockquote><hr>

<p>

<a name="heading_14.8.1.1"><h3>14.8.1.1 FIELD Expressions</h3></a>
<a name="index_x_941"></a>
<a name="index_x_942"></a>

<p>
The <b>field_expr</b> used in the 
<b>ASK STRUCTURE FIELD: item</b> statement can be either a 
<i>constant</i> or a <i>string or numeric expression</i>. 

<p>
<a name="index_x_943"></a>
You can use a string constant to specify the field name.  To use a 
string constant, just enter the field name, <i>without quotes</i>. 
INTOUCH will then use the string constant as the field name: 

<p><pre>
        ASK STRUCTURE TTI_RUN:CLIENT, FIELD LAST: DESCRIPTION A$ 
                                        / 
                  the field is specified by its field name 
</pre>

<p>
You can also specify a field name with an expression.  To use an 
expression, precede the expression with a pound sign (#).  The pound 
sign tells INTOUCH that the following characters are an expression, 
not the field name.  If you do not include a pound sign, INTOUCH will 
interpret the characters as a field name. 

<p><pre>
        ASK STRUCTURE CL, FIELD #FIELDNAME$: DESCRIPTION A$ 
                                    / 
          the field is specified by the value of the variable FIELDNAME$ 
 
 
        ASK STRUCTURE CL, FIELD #FIELDNUM: DESCRIPTION A$ 
                                    / 
          the field is specified by the value of the variable FIELDNUM 
</pre>

<h2>EXAMPLE:</h2>

<p>
This example shows how you can access the actual field data using 
field expressions. 

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client' 
        20  DO 
            CLEAR 
            PRINT AT 1, 1:; 
              ask_fields 
              IF  _BACK  OR  _EXIT  THEN  EXIT DO 
              show_data 
            LOOP 
            CLOSE STRUCTURE cl 
            STOP 
 
        30  ask_fields: 
            DO 
              IF  _ERROR  THEN  SET ERROR OFF 
              PRINT 'Field List:  '; &amp; 
                      'ID, LAST, FIRST, MIDDLE, STREET, CITY, STATE, ZIP, PHONE' 
              PRINT 
              LINE INPUT 'Select fields to display': field_list$ 
              IF  _BACK  OR  _EXIT  THEN  EXIT DO 
              FOR f = 1 TO ELEMENTS(field_list$) 
                field$ = ELEMENT$(field_list$, f) 
                ASK STRUCTURE cl, FIELD #field$: NUMBER z 
                IF  z = 0  THEN 
                  MESSAGE ERROR: 'Illegal field: '; field$ 
                END IF 
              NEXT f 
        40  LOOP WHILE _ERROR 
            RETURN 
 
        50  show_data: 
            PRINT 
            EXTRACT STRUCTURE cl 
              FOR f = 1 TO ELEMENTS(field_list$) 
                field$ = ELEMENT$(field_list$, f) 
                PRINT cl(#field$), 
              NEXT f 
              PRINT 
        60  END EXTRACT 
            DELAY 
            RETURN 
        70  END 
 
        RNH 
        Field List:  ID, LAST, FIRST, MIDDLE, STREET, CITY, STATE, ZIP, PHONE 
 
        Select fields to display? last,first,phone 
 
        Smith               Sam                 (809) 555-8789 
        Kent                Keith               (619) 967-5021 
        Johnson             Paul                (619) 489-5551 
        Waters              Wayne               (619) 564-1231 
        Rodrigues           Homero              (   )    -   0 
        Donaldson           George              (   )    -   0 
        Errant              Earl                (408) 844-7676 
        Abott               Al                  (202) 566-9892 
        Brock               Bud                 (218) 555-4322 
        Cass                Cathy               (619) 743-8582 
        Porter              Pete                (619) 778-6709 
        Derringer           Dale                (818) 223-9014 
        Farmer              Fred                (305) 552-7872 
</pre>

<a name="heading_14.8.1.2"><h3>14.8.1.2 Item: ACCESS</h3></a>
<a name="index_x_944"></a>
<a name="index_x_945"></a>
<a name="index_x_946"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_name: ACCESS str_var 
</pre>

<p>
<b>ACCESS</b> retrieves the access (read and write) rules for the 
specified field.  This information tells you if the field can be read and 
written to.  <b>N</b> is normal--meaning the field can be read 
and written to if the structure is also "N"ormal.  Depending on whether 
security levels have been set on the structure and/or field, the letter 
can be in the range of A thru Z.  INTOUCH defaults to N when the structure 
is created and fields are defined. 

<p><pre>
        10  OPEN STRUCTURE inv: NAME 'tti_run:invoice', ACCESS INPUT 
        20  ASK STRUCTURE inv, FIELD custnbr: ACCESS x$ 
        30  PRINT x$ 
        40  CLOSE STRUCTURE inv 
        50  END 
 
        RNH 
        READ:N, WRITE:N 
</pre>

<a name="heading_14.8.1.3"><h3>14.8.1.3 Item: APPLICATION</h3></a>
<a name="index_x_947"></a>
<a name="index_x_948"></a>
<a name="index_x_949"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: APPLICATION str_var 
</pre>

<p>
<b>APPLICATION</b> returns a name of an application for the 
specified field in a string variable.  This is optional information 
the user provides when the field is defined. 

<p><pre>
        10  OPEN STRUCTURE cl : NAME 'tti_run:client' 
            ASK STRUCTURE cl, FIELD id: APPLICATION str$ 
            PRINT str$ 
        20  END 
 
        RNH 
        REPORTING_ID 
</pre>

<a name="heading_14.8.1.4"><h3>14.8.1.4 Item: ATTRIBUTES</h3></a>
<a name="index_x_950"></a>
<a name="index_x_951"></a>
<a name="index_x_952"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: ATTRIBUTES str_var 
</pre>

<p>
<b>ATTRIBUTES</b> returns the INTOUCH field semantics 
(NUM - number, UC - upper-case, etc.) for the specified field in a 
string variable. 

<p>
You can refer to Section 16.4.8, Semantics for detailed information 
on field attributes. 

<p>

<a name="heading_14.8.1.5"><h3>14.8.1.5 Item: CHANGEABLE</h3></a>
<a name="index_x_953"></a>
<a name="index_x_954"></a>
<a name="index_x_955"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: CHANGEABLE num_var 
</pre>

<p>
<b>CHANGEABLE</b> returns a value of TRUE or FALSE.  If the 
field specified by <i>field_expr</i> can be changed, the value is TRUE. 
If the field cannot be changed, the value is FALSE. 

<p><pre>
        10  OPEN STRUCTURE cl: NAME 'tti_run:client' 
            ASK STRUCTURE cl, FIELD id: CHANGEABLE z 
            ASK STRUCTURE cl, FIELD city: CHANGEABLE z1 
            PRINT z 
            PRINT z1 
            CLOSE STRUCTURE CL 
        20  END 
 
        RNH 
         0 
         1 
</pre>

<a name="heading_14.8.1.6"><h3>14.8.1.6 Item: CLASSIFICATION</h3></a>
<a name="index_x_956"></a>
<a name="index_x_957"></a>
<a name="index_x_958"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: CLASSIFICATION str_var 
</pre>

<p>
<b>CLASSIFICATION</b> returns the classification (if there is one) 
for the specified field in a string variable.  This is optional information. 

<p><pre>
        10  OPEN STRUCTURE cl : NAME 'tti_run:client' 
            ASK STRUCTURE cl, FIELD last: CLASSIFICATION str$ 
            PRINT str$ 
        20  END 
 
        RNH 
        NAME 
</pre>

<a name="heading_14.8.1.7"><h3>14.8.1.7 Item: DATATYPE</h3></a>
<a name="index_x_959"></a>
<a name="index_x_960"></a>
<a name="index_x_961"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: DATATYPE str_var 
</pre>

<p>
<b>DATATYPE</b> returns the field data type, such as CH (character), 
IN (integer), etc., in a string variable. 

<p>
You can refer to Section 16.4.4, Data type for detailed information 
on field data types. 

<p>

<a name="heading_14.8.1.8"><h3>14.8.1.8 Item: DESCRIPTION</h3></a>
<a name="index_x_962"></a>
<a name="index_x_963"></a>
<a name="index_x_964"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: DESCRIPTION str_var 
</pre>

<p>
<b>DESCRIPTION</b> returns the description for the specified 
field in a string variable. 

<p>

<a name="heading_14.8.1.9"><h3>14.8.1.9 Item: HEADING</h3></a>
<a name="index_x_965"></a>
<a name="index_x_966"></a>
<a name="index_x_967"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: HEADING str_var 
</pre>

<p>
<b>HEADING</b> returns the report column heading for the 
specified field in a string variable.  This is the heading that would 
appear in a Guided Query Language (GQS) report column. 

<p>

<a name="heading_14.8.1.10"><h3>14.8.1.10 Item: HELP</h3></a>
<a name="index_x_968"></a>
<a name="index_x_969"></a>
<a name="index_x_970"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: HELP str_var 
</pre>

<p>
<b>HELP</b> returns the help text for the specified field in 
a string variable. 

<p>

<a name="heading_14.8.1.11"><h3>14.8.1.11 Item: KEYED</h3></a>
<a name="index_x_971"></a>
<a name="index_x_972"></a>
<a name="index_x_973"></a>

<p><pre>
        ASK STRUCTURE struc_name, FIELD field_expr: KEYED num_var 
</pre>

<p>
<b>KEYED</b> returns a value of TRUE or FALSE in a numeric variable. 
If the specified field is a key field, the value is TRUE.  Otherwise, the 
value is FALSE. 

<p>
<a name="bottom_015"></a>
<p>
<hr> <a href="language_016.html">Next page...</a> | 
<a href="language_contents.html">Table of Contents</a>
</body>
</html>
