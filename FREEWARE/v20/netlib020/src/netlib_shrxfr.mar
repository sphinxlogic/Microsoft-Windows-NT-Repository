	.TITLE	NETLIB_SHRXFR
	.IDENT	/V2.0/
;
; FACILITY: 	NETLIB
;
; ABSTRACT: 	tbs
;
; AUTHOR:   	M. Madison
;
; MODIFICATION HISTORY:
;
;   24-OCT-1994	V2.0	Madison	    All new for V2.0.
;
	.NTYPE	R22TYPE,R22		; get symbol type for "R22"
R22TYPE	=	<R22TYPE@-4&^XF>-5	; type 5 = register
	.IF	EQ,R22TYPE
EVAX	=	1
BIGPAGE	=	1
ADDRESSBITS =	32
	.ENDC

    	.DISABLE GLOBAL
	.EXTRN	LIB$FIND_IMAGE_SYMBOL

	.PSECT	$$$COPYRIGHT,SHR,NOEXE,RD,NOWRT,PIC,GBL,QUAD
	.ASCII	/Copyright © 1994, MadGoat Software.  All Rights Reserved./

	.PSECT	DATAPSECT,NOSHR,NOEXE,RD,WRT,PIC,GBL,QUAD
IMGNAM:	.ASCID	/NETLIB_SHR/
	.PSECT	CODEPSECT,SHR,EXE,RD,NOWRT,PIC,GBL,QUAD
;
; Macro to generate a transfer vector entry.
;
	.MACRO	ROUTINE	NAME
	.PSECT	DATAPSECT
	.ALIGN	LONG
NAME'_PTR:
	.LONG	0
NAME'_STR:
	.ASCID	/NAME/
	.PSECT	CODEPSECT
	.IF	DF,EVAX
NAME::	.CALL_ENTRY MAX_ARGS=14,HOME_ARGS=TRUE
	.IFF
	.ENTRY	NAME,^M<>
	.ENDC
	TSTL	NAME'_PTR
	BNEQ	5$
	PUSHAL	NAME'_PTR
	PUSHAQ	NAME'_STR
	PUSHAQ	IMGNAM
	CALLS	#3,G^LIB$FIND_IMAGE_SYMBOL
	BLBC	R0,10$
5$:	CALLG	(AP),@NAME'_PTR
10$:	RET
	.ENDM

;
;   Old routines - for compatibility with V1
;
	ROUTINE	NET_ASSIGN
	ROUTINE	NET_BIND
	ROUTINE	NET_DEASSIGN
	ROUTINE	NET_GET_ADDRESS
	ROUTINE	NET_ADDR_TO_NAME
	ROUTINE	NET_GET_INFO
	ROUTINE	NET_GET_HOSTNAME
;
    	ROUTINE	TCP_CONNECT
	ROUTINE TCP_CONNECT_ADDR
    	ROUTINE	TCP_DISCONNECT
    	ROUTINE TCP_ACCEPT
    	ROUTINE TCP_SEND
	ROUTINE	TCP_RECEIVE
    	ROUTINE	TCP_GET_LINE
;
	ROUTINE	UDP_SEND
	ROUTINE	UDP_RECEIVE

	ROUTINE	DNS_MXLOOK

	ROUTINE	NET_SET_TRACE_ROUTINE
;
;   New, Improved Routines !
;
	ROUTINE	NETLIB_SOCKET
	ROUTINE	NETLIB_SERVER_SETUP
	ROUTINE	NETLIB_BIND
	ROUTINE	NETLIB_GETSOCKNAME
	ROUTINE	NETLIB_GETPEERNAME
	ROUTINE	NETLIB_CONNECT
	ROUTINE	NETLIB_WRITE
    	ROUTINE	NETLIB_WRITELINE
	ROUTINE NETLIB_READ
    	ROUTINE	NETLIB_READLINE
	ROUTINE	NETLIB_SHUTDOWN
    	ROUTINE NETLIB_CLOSE
    	ROUTINE NETLIB_LISTEN
	ROUTINE	NETLIB_ACCEPT
    	ROUTINE NETLIB_GET_HOSTNAME
	ROUTINE	NETLIB_SETSOCKOPT
	ROUTINE	NETLIB_GETSOCKOPT
    	ROUTINE NETLIB_NAME_TO_ADDRESS
    	ROUTINE NETLIB_ADDRESS_TO_NAME
    	ROUTINE NETLIB_DNS_QUERY
    	ROUTINE NETLIB_DNS_SKIPNAME
    	ROUTINE NETLIB_DNS_EXPANDNAME
    	ROUTINE NETLIB_STRTOADDR
	ROUTINE NETLIB_ADDRTOSTR
    	ROUTINE NETLIB_CONNECT_BY_NAME
    	ROUTINE	NETLIB_DNS_MX_LOOKUP
	ROUTINE	NETLIB_HTON_LONG
	ROUTINE	NETLIB_NTOH_LONG
	ROUTINE	NETLIB_HTON_WORD
	ROUTINE	NETLIB_NTOH_WORD

	.END
