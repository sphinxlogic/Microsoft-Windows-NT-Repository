DELETE CONVERT_PARAMETERS;
REDEFINE PROCEDURE CONVERT_PARAMETERS
!
! Convert SYSGEN parameters from text to "normalized" numbers.
! TPARAMS must first be defined to read the correct file.
! Only numeric parameters are converted.
!
! B. Z. Lederman
!
READY TPARAMS
DEFINE FILE FOR PARAMS;	! remember to purge old files
READY PARAMS WRITE
!
FOR TPARAMS WITH UNIT NE "Ascii", "Unit  Dynami", " "
    STORE PARAMS USING BEGIN
	NAME = NAME
	CURRENT = CURRENT
	DEFAULT = DEFAULT
	MINIMUM = MINIMUM
	MAXIMUM = MAXIMUM
	UNIT = UNIT
	IF TPARAMS_REC.DYNAMIC = "D" THEN DYNAMIC = DYNAMIC ELSE DYNAMIC = " "
    END
END_PROCEDURE

DELETE DISK6;
REDEFINE DOMAIN DISK6 OF DISK6_IN USING
01 IDENTIFICATION OCCURS FOR DISK6_IN WITH TYPE = 1.
    10 SERIAL_NUMBER FROM DISK6_IN.
    10 STRUCTURE_NAME FROM DISK6_IN.
    10 VOLUME_NAME FROM DISK6_IN.
    10 OWNER_NAME FROM DISK6_IN.
    10 FORMAT FROM DISK6_IN.
    10 CREATION_DATE FROM DISK6_IN.
    10 CREATION_TIME FROM DISK6_IN.
    10 FILE_RECORD OCCURS FOR DISK6_IN WITH TYPE = 2.
	20 FILEOWNER FROM DISK6_IN.
	20 NUMERIC FROM DISK6_IN.
	20 IDENTIFIER FROM DISK6_IN.
	20 ALLOCATED FROM DISK6_IN.
	20 USED FROM DISK6_IN.
	20 FILESPEC FROM DISK6_IN.
;

DELETE DISK6_IN;
REDEFINE DOMAIN DISK6_IN USING DISK_USE_RECORD ON SYSDISK.USE;

DELETE DISK_USE_RECORD;
REDEFINE RECORD DISK_USE_RECORD OPTIMIZE
01 DISK_USE_REC.
    10 TYPE USAGE BYTE.
    10 COVERUP.
	20 FILLER PIC X(120).
    10 IDENTIFICATION REDEFINES COVERUP.
	20 SERIAL_NUMBER USAGE LONG.
	20 STRUCTURE_NAME PIC X(12).
	20 VOLUME_NAME PIC X(12).
	20 OWNER_NAME PIC X(12).
	20 FORMAT PIC X(12).
	20 CREATION_DATE USAGE DATE.
	20 CREATION_TIME COMPUTED BY FN$TIME(CREATION_DATE).
    10 FILE_RECORD REDEFINES COVERUP.
	20 FILEOWNER USAGE LONG.
!!	20 NUMERIC COMPUTED BY FN$FAO("!%U", FILEOWNER, 0,0,0,0,0,0,0)
!!	    EDIT_STRING X(9).
!!	20 IDENTIFIER COMPUTED BY FN$FAO("!%I", FILEOWNER, 0,0,0,0,0,0,0)
!!	    EDIT_STRING X(24).
	20 ALLOCATED USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
	20 USED USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
	20 DIR_LEN USAGE WORD.
	20 SPEC_LEN USAGE WORD.
	20 FILESPEC PIC X(96) EDIT_STRING T(76).
	20 DIRECTORY COMPUTED BY
	    FORMAT FN$STR_EXTRACT(FILESPEC, 1, DIR_LEN) USING X(64)
	    EDIT_STRING T(64).
	20 FILE_SPECIFICATION COMPUTED BY
            FORMAT FN$STR_EXTRACT(FILESPEC, (DIR_LEN + 1), SPEC_LEN) USING X(64)
	    EDIT_STRING T(64).
;

DELETE DISK_USE_REPORT;
REDEFINE PROCEDURE DISK_USE_REPORT
REPORT DK WITH TYPE = 2 SORTED BY FILESPEC ON *."file name"
AT BOTTOM OF DIRECTORY PRINT COL 1,
    TOTAL(ALLOCATED) USING ZZZ,ZZ9, SPACE 1,
    DIRECTORY USING T(64)
AT BOTTOM OF REPORT PRINT SKIP, TOTAL(ALLOCATED) USING ZZZ,ZZ9
END_REPORT
END_PROCEDURE

DELETE ERRLOG_REC;
REDEFINE RECORD ERRLOG_REC OPTIMIZE
01 ERRLOG_REC.
    10 FILLER USAGE LONG. ! SID EDIT_STRING ZZZ,ZZZ,ZZZ9.
    10 TYPE USAGE WORD EDIT_STRING ZZZZ9.
    10 PACKET COMPUTED BY TYPE VIA ERRLOG_TYPE_TABLE.
    10 LOG_DAT USAGE DATE.
    10 LOG_TIM COMPUTED BY FN$TIME(LOG_DAT) EDIT_STRING X(10).
    10 SEQ USAGE WORD EDIT_STRING ZZZZ9.
    10 FILLER PIC X(248). ! EDIT_STRING T(64).
;

DELETE ERRLOG_TYPE_TABLE;
REDEFINE TABLE ERRLOG_TYPE_TABLE
  "6" : "Corrected Memory Error",
 "35" : "Created ERRLOG.SYS",
 "38" : "Time Stamp",
 "64" : "Mount Volume",
 "65" : "Dismount Volume",
"100" : "ERL$LOGMESSAGE"
ELSE "Undefined"
END_TABLE

DELETE ERROR_LOG;
REDEFINE DOMAIN ERROR_LOG USING ERRLOG_REC ON SYS$ERRORLOG:ERRLOG.SYS;

DELETE FILES;
REDEFINE DOMAIN FILES USING FILES_RECORD ON SYSDISK.TMP;

DELETE FILES_RECORD;
REDEFINE RECORD FILES_RECORD OPTIMIZE
01 FILE_REC.
    10 PROCESS_NAME PIC X(14).
    10 FILLER PIC XX.
    10 PID PIC X(8).
    10 FILLER PIC XX.
    10 FILE_NAME PIC X(64) EDIT_STRING T(48).
;

DELETE MAIL;
REDEFINE DOMAIN MAIL USING VMSMAIL_RECORD ON SYS$SYSTEM:VMSMAIL.DAT;

DELETE NETPRF_REC;
REDEFINE RECORD NETPRF_REC OPTIMIZE
01 NETPRF_REC.
	10 NETWORK PIC X(9).
	10 STUFF OCCURS 20 TIMES.
		20 A USAGE LONG.
	10 FILLER PIC X(200).
;

DELETE PARAMETER_TEXT;
REDEFINE DOMAIN PARAMETER_TEXT USING PARAMETER_TEXT_RECORD ON
    SYS$HELP:SYSGEN_PARAMETERS.DOM;

DELETE PARAMETER_TEXT_RECORD;
REDEFINE RECORD PARAMETER_TEXT_RECORD
01 PARAMETER_TEXT_REC.
    10 PARAMETER_NAME PIC X(16).
    10 DESCRIPTION PIC X(255) EDIT_STRING T(32).
;

DELETE PARAMS;
REDEFINE DOMAIN PARAMS USING PARAMS_RECORD ON PARAMS.SEQ;

DELETE PARAMS_RECORD;
REDEFINE RECORD PARAMS_RECORD
!
! Write out "normalized" SYSGEN parameters.
!
! B. Z. Lederman
!
01 PARAMS_REC.
    10 NAME PIC X(16).
    10 CURRENT USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
    10 DEFAULT USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
    10 MINIMUM USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
    10 MAXIMUM USAGE LONG EDIT_STRING ZZZ,ZZZ,ZZ9.
    10 UNIT PIC X(12).
    10 DYNAMIC PIC X.
;

DELETE PARAM_TEXT;
REDEFINE TABLE PARAM_TEXT FROM DOMAIN PARAMETER_TEXT
       EDIT_STRING T(32)
       USING PARAMETER_NAME : DESCRIPTION,
       ELSE "Undefined"
       END_TABLE

DELETE PRF;
REDEFINE DOMAIN PRF USING NETPRF_REC ON NETPRF.DAT;

DELETE PRINT_DISK6;
REDEFINE PROCEDURE PRINT_DISK6
:T132
FOR DISK6_IN BEGIN
    IF TYPE = 1
      THEN PRINT IDENTIFICATION ELSE PRINT NUMERIC,
	IDENTIFIER, ALLOCATED, USED, DIRECTORY,
	FILE_SPECIFICATION
END
END_PROCEDURE

DELETE PRINT_PARAMETERS;
REDEFINE PROCEDURE PRINT_PARAMETERS
!
! Print out the normalized parameters in "ADD_" format.
! Values are current minus default.
!
! B. Z. Lederman
!
FOR PARAMS WITH CURRENT NE DEFAULT SORTED BY NAME
    PRINT "ADD_" | NAME ||| "=" |||
	(CURRENT - DEFAULT) ON *."TT or filespec"
END_PROCEDURE

DELETE REPORT_DISK6;
REDEFINE PROCEDURE REPORT_DISK6
REPORT DISK6_IN WITH TYPE = 2 ON *."TT or file name"
    AT BOTTOM OF FILEOWNER PRINT NUMERIC, IDENTIFIER,
	COUNT("Number-of-files") USING ZZZ,ZZ9,
	TOTAL(ALLOCATED) USING ZZZ,ZZZ,ZZ9,
	TOTAL(USED) USING ZZZ,ZZZ,ZZ9
END_REPORT
END_PROCEDURE

DELETE RIGHTS;
REDEFINE DOMAIN RIGHTS USING RIGHTS_RECORD ON SYS$SYSTEM:RIGHTSLIST.DAT;

DELETE RIGHTS_RECORD;
REDEFINE RECORD RIGHTS_RECORD OPTIMIZE
01 RIGHTS_REC.
!
! Quick summaries of rights lists, etc.  Any use of this definition
!	to modify the rights list is done at your own peril.  You
!	should also remember to READY SHARED if your default insn't
!	set to shared.
!
! B. Z. Lederman    15-Apr-1987
! 		    29-Dec-1987	Show name or rights holder
!
    10 HIDE_IDENT.
	20 FILLER PIC XXXX.
    10 IDENT REDEFINES HIDE_IDENT.
	20 IDENT_IN USAGE LONG.
!
! The Identifier is in UIC format.  Using the added FAO function
! this can be formatted in much neater ways than a decimal long word.
!
!!    10 NUMERIC COMPUTED BY FN$FAO("!%U", IDENT_IN, 0,0,0,0,0,0,0)
!!	    EDIT_STRING X(9).
!!    10 IDENTIFIER COMPUTED BY FN$FAO("!%I", IDENT_IN, 0,0,0,0,0,0,0)
!!	    EDIT_STRING X(22).
    10 FILLER PIC X(4).
    10 HIDE_HOLDER.
	20 FILLER PIC XXXX.
    10 HOLD REDEFINES HIDE_HOLDER.
	20 HOLD_IN USAGE LONG.
    10 FILLER PIC X(4).
    10 HIDE_NAME.
	20 FILLER PIC X(32).
    10 RN REDEFINES HIDE_NAME.
	20 NAME PIC X(32).
    10 FILLER PIC X(16).    ! just to stop record length errors
!
! It would be easier to just print out the holder in columns, and
! the name in columns.  I'm doing it this way because:
!	a) if there is a holder there isn't a name
!	b) this way it fits into 80 columns
!
!    10 NAME_OR_HOLDER COMPUTED BY CHOICE OF
!	    (HOLD_IN NE 0) THEN
!		(FN$FAO("!%U", HOLD_IN, 0,0,0,0,0,0,0) |	! notice the
!		 FN$FAO("!%I", HOLD_IN, 0,0,0,0,0,0,0))		! concatenation
!	    ELSE NAME
!	    END_CHOICE
!	    EDIT_STRING X(32).
;

DELETE SHORT_SYSUAF;
REDEFINE DOMAIN SHORT_SYSUAF USING SHORT_SYSUAF_RECORD ON SYSUAF.SEQ;

DELETE SHORT_SYSUAF_RECORD;
REDEFINE RECORD SHORT_SYSUAF_RECORD USING OPTIMIZE
01 SHORT_SYSUAF_REC.
	03 USERNAME PIC X(32).
	03 ACCOUNT PIC X(8).
	03 OWNER PIC X(31).
	03 DIRECTORY PIC X(63).
	03 LOGFAILS USAGE WORD.
	03 DATE_LAST_INTERACTIVE_LOGIN USAGE DATE QUERY_NAME IS DLIL.
	03 TIME_LAST_INTERACTIVE_LOGIN COMPUTED BY FN$TIME(DLIL).
;

DELETE SYSUAF;
REDEFINE DOMAIN SYSUAF USING SYSUAF_RECORD ON SYS$SYSTEM:SYSUAF.DAT;

DELETE SYSUAF_RECORD;
REDEFINE RECORD SYSUAF_RECORD USING OPTIMIZE
01 SYSUAF.
	03 FILLER PIC X(4).
	03 USERNAME PIC X(32).
	03 UIC_WORDS.
		05 MEMBER USAGE WORD.
		05 GROUP USAGE WORD.
	03 FILLER PIC X(12).
	03 ACCOUNT PIC X(8).
	03 FILLER PIC X(24).
	03 OWNER_FIELD_LENGTH USAGE BYTE.
	03 OWNER PIC X(31).
	03 DEVICE_FIELD_LENGTH USAGE BYTE.
	03 DEVICE PIC X(31).
	03 DIRECTORY_FIELD_LENGTH USAGE BYTE.
	03 DIRECTORY PIC X(63).
	03 LGICMD_FIELD_LENGTH USAGE BYTE.
	03 LGICMD PIC X(63).
	03 CLI_FIELD_LENGTH USAGE BYTE.
	03 CLI PIC X(31).
	03 TABLES_FIELD_LENGTH USAGE BYTE.
	03 TABLES PIC X(31).
	03 PASSWORD_DEPENDENT USAGE QUAD.
	03 FILLER USAGE QUAD.
	03 LOGFAILS USAGE WORD.
	03 PWDA USAGE LONG.
	03 PWDMINIMUM USAGE BYTE.
	03 PWDB USAGE BYTE.
	03 EXPIRATION USAGE DATE.
	03 TIME_EXPIRATION COMPUTED BY FN$TIME(EXPIRATION).
	03 FILLER PIC X(8).
	03 PWDCHANGE USAGE DATE.
	03 FILLER PIC X(8).
	03 DATE_LAST_INTERACTIVE_LOGIN USAGE DATE QUERY_NAME IS DLIL.
	03 TIME_LAST_INTERACTIVE_LOGIN COMPUTED BY FN$TIME(DLIL).
	03 DATE_LAST_NONINTERACTIVE_LOGIN USAGE DATE QUERY_NAME IS DLNL.
	03 TIME_LAST_NONINTERACTIVE_LOGIN COMPUTED BY FN$TIME(DLNL).
	03 AUTHORIZE_PRIV_QUADWORD USAGE QUAD.
	03 DEFAULT_PRIV_QUADWORD USAGE QUAD.
	03 FILLER PIC X(40).
	03 LOGIN_FLAGS_WORD USAGE WORD.
	03 FILLER PIC X(2).
	03 NETWORK_ACCESS_BYTES OCCURS 6 TIMES.
		05 NETWORK_ACCESS_BYTE USAGE BYTE.
	03 BATCH_ACCESS_BYTES OCCURS 6 TIMES.
		05 BATCH_ACCESS_BYTE USAGE BYTE.
	03 LOCAL_ACCESS_BYTES OCCURS 6 TIMES.
		05 LOCAL_ACCESS_BYTE USAGE BYTE.
	03 DIALUP_ACCESS_BYTES OCCURS 6 TIMES.
		05 DIALUP_ACCESS_BYTE USAGE BYTE.
	03 REMOTE_ACCESS_BYTES OCCURS 6 TIMES.
		05 REMOTE_ACCESS_BYTE USAGE BYTE.
	03 FILLER PIC X(12).
	03 PRIME_DAYS_BITS USAGE BYTE.
	03 FILLER USAGE BYTE.
	03 PRIO USAGE BYTE.
	03 QUEPRIO USAGE BYTE.
	03 MAXJOBS USAGE WORD.
	03 MAXACCTJOBS USAGE WORD.
	03 MAXDETACH USAGE WORD.
	03 PRCLM USAGE WORD.
	03 DIOLM USAGE WORD.
	03 BIOLM USAGE WORD.
	03 TQELM USAGE WORD.
	03 ASTLM USAGE WORD.
	03 ENQLM USAGE WORD.
	03 FILLM USAGE WORD.
	03 SHRFILLM USAGE WORD.
	03 WSQUO USAGE LONG.
	03 WSDEF USAGE LONG.
	03 WSEXT USAGE LONG.
	03 PGFLQUO USAGE LONG.
	03 CPU USAGE LONG.
	03 BYTLM USAGE LONG.
	03 PBYTLM USAGE LONG.
	03 JTQUO USAGE LONG.
	03 FILLER PIC X(844).
;

DELETE T132;
REDEFINE PROCEDURE T132
SET COLUMNS_PAGE=132
FN$WIDTH(132)
![?3h
END_PROCEDURE

DELETE T80;
REDEFINE PROCEDURE T80
SET COLUMNS_PAGE=80
FN$WIDTH(80)
![?3l
END_PROCEDURE

DELETE TPARAMS;
REDEFINE DOMAIN TPARAMS USING TPARAMS_RECORD ON PARAMS.07MAR89;

DELETE TPARAMS_RECORD;
REDEFINE RECORD TPARAMS_RECORD
01 TPARAMS_REC.
!
! Read in a "Raw" text file output of parameters from SYSGEN
!
! B. Z. Lederman
!
    10 NAME PIC X(16).
    10 CURRENT PIC X(18).
    10 SUBCUR REDEFINES CURRENT.
	20 FILLER PIC X(8).
	20 RCUR PIC X(10).
    10 DEFAULT PIC X(10).
    10 MINIMUM PIC X(10).
    10 MAXIMUM PIC X(10).
    10 FILLER PIC X.
    10 UNIT PIC X(12).
    10 DYNAMIC PIC X.
;

DELETE VMSMAIL_RECORD;
REDEFINE RECORD VMSMAIL_RECORD OPTIMIZE
01 VMSMAIL_REC.
    10 USER_NAME PIC X(31).
    10 FILLER PIC XX.
    10 UNREAD_COUNT USAGE WORD EDIT_STRING Z,ZZ9.
    10 FILLER PIC X(31).
    10 LEN USAGE WORD EDIT_STRING ZZ9.
    10 FULL_NAME PIC X(31).
;

