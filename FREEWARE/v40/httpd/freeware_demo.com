$! FREEWARE_DEMO.COM
$! 10-JAN-96  MGD  v1.0.0, initial development
$ VERIFIED = F$VERIFY(0)
$ ON ERROR THEN GOTO EXITING
$ ON CONTROLY THEN GOTO EXITING
$ SAY = "WRITE SYS$OUTPUT"
$ SAY ""
$ SAY "After the server startup use a browser to access " + -
      "http://''f$GETSYI("NODENAME")':8880/"
$ SAY ""
$ SAY "When finished evaluation abort server execution using control-Y"
$ SAY "(a subprocess will be spawned to preserve current privileges and process name)"
$ SAY ""
$ PROCEDURE = F$ENVIRONMENT("PROCEDURE")
$ DEVICE = F$PARSE(PROCEDURE,,,"DEVICE","NO_CONCEAL") - ":"
$ DIRECTORY = F$PARSE(PROCEDURE,,,"DIRECTORY","NO_CONCEAL")
$!(sigh, allow for concealed device, what a kludge!)
$ DIRECTORY = DIRECTORY - "[" - "[" - "]" - "]" - ".000000"
$ DEFINE /JOB /TRANSLATION=CONCEALED HT_ROOT 'DEVICE':['DIRECTORY'.]
$ IF ARCHITECTURE .EQS. "VAX"
$ THEN
$    EXE_ROOT = F$TRNLNM("HT_ROOT") - ".]" + ".VAX.]"
$    DEFINE /JOB /TRANSLATION=CONCEALED HT_EXE_ROOT 'EXE_ROOT'
$    DEFINE /JOB HT_EXE HT_EXE_ROOT:[000000]
$ ELSE
$    EXE_ROOT = F$TRNLNM("HT_ROOT") - ".]" + ".AXP.]"
$    DEFINE /JOB /TRANSLATION=CONCEALED HT_EXE_ROOT 'EXE_ROOT'
$    DEFINE /JOB HT_EXE HT_EXE_ROOT:[000000]
$ ENDIF
$ DEFINE /JOB HTTPD$CONFIG HT_ROOT:[EXAMPLE]HTTPD$CONFIG.CONF
$ DEFINE /JOB HTTPD$MAP HT_ROOT:[EXAMPLE]HTTPD$MAP.CONF
$ DEFINE /JOB HTTPD$GMT "+09:30"
$ IF F$TRNLNM("MULTINET") .EQS. ""
$    THEN HTTPD = "$HT_EXE:HTTPD"
$    ELSE HTTPD = "$HT_EXE:HTTPD_MULTINET"
$ ENDIF
$ SPAWN /WAIT HTTPD /NOLOG /NOMONITOR /PORT=8880
$ EXITING:
$ DEASSIGN /JOB HT_ROOT
$ DEASSIGN /JOB HT_EXE
$ DEASSIGN /JOB HTTPD$CONFIG
$ DEASSIGN /JOB HTTPD$MAP
$ DEASSIGN /JOB HTTPD$GMT
$ SAY ""
$ IF .NOT. VERIFIED THEN SET NOVERIFY
