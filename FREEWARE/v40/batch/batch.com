$ OLD_VFY = 'F$VERIFY(0)'
$ IF F$MODE() .EQS. "BATCH" THEN GOTO BATCH
$ IF F$MODE() .NES. "INTERACTIVE" THEN EXIT
$ !
$ ! Submit single command as batch job -- Tim Halvorsen
$ ! Updated to V2.0 -- Dave Porter 18-Oct-1985
$ !  (copy proc privs, set proc name of job, allow V4 param lengths, 
$ !   clean up to use V4 DCL capabilities)
$ !
$ JOB = F$EDIT(P1,"UPCASE,COLLAPSE")
$ JOB = F$ELEMENT(0,"/",JOB)
$ IF F$EXTRACT(0,1,JOB) .EQS. "@" THEN -
  JOB = F$PARSE(F$EXTRACT(1,255,JOB),,,"NAME")
$ JOB = F$EXTRACT(0,15,JOB)
$ IF JOB .EQS. "" THEN JOB = "BATCH"
$
$ PROC    = F$ENVIRONMENT("PROCEDURE")
$ DELQUAL = "/NODELETE"
$ COMMAND = F$EDIT("''P1' ''P2' ''P3' ''P4' ''P5' ''P6' ''P7' ''P8'","COMPRESS,TRIM")
$
$ ORIGDIR = F$ENVIRONMENT("DEFAULT")
$
$ ORIGPRV = F$GETJPI(0,"PROCPRIV")
$ IF F$LEN(ORIGPRV) .GT. 255-6 THEN GOTO SQUISHPRV
$10:
$ IF ORIGPRV .NES. "" THEN ORIGPRV = "NOALL,"+ORIGPRV
$ IF ORIGPRV .EQS. "" THEN ORIGPRV = "NOALL"
$
$ IF F$LEN(COMMAND)+F$LEN(JOB)+F$LEN(ORIGDIR)+F$LEN(ORIGPRV) .LE. 480 -
  THEN GOTO SUBMIT
$
$ WRITE SYS$OUTPUT "%BATCH-I-TMPFIL, using temporary file ''JOB'.TMP"
$ OPEN/WRITE TEMPCOM SYS$SCRATCH:'JOB'.TMP
$ WRITE TEMPCOM "$ ",COMMAND
$ CLOSE TEMPCOM
$ DELQUAL = "/DELETE"
$ COMMAND = "@SYS$SCRATCH:''JOB'.TMP"
$
$ IF F$LEN(COMMAND)+F$LEN(JOB)+F$LEN(ORIGDIR)+F$LEN(ORIGPRV) .LE. 480 -
  THEN GOTO SUBMIT
$
$ WRITE SYS$OUTPUT "%BATCH-E-TOOMUCH, too much data for submit parameters" 
$ EXIT %X10000002+(0*F$VERIFY(OLD_VFY))
$
$SUBMIT:
$ SUBMIT/NOPRINTER/NOTIFY'DELQUAL'-
	/NAME='JOB'-
	/PARAMETERS=("''COMMAND'","''JOB'","''ORIGDIR'","''ORIGPRV'")-
	'PROC'
$ EXIT $STATUS+(0*F$VERIFY(OLD_VFY))
$
$SQUISHPRV:
$ TMP = ""
$ N = 0
$SQ10:
$ PRV = F$ELEMENT(N,",",ORIGPRV)
$ IF PRV .EQS. "," THEN GOTO SQ20
$ TMP = TMP + "," + F$EXT(0,4,PRV)
$ N = N + 1
$ GOTO SQ10
$SQ20:
$ ORIGPRV = TMP - ","
$ DELETE/SYMBOL TMP
$ GOTO 10
$
$BATCH:
$ SET NOON
$ SET PROCESS/NAME='P2'
$ IF OLD_VFY THEN SET VERIFY
$ SET DEFAULT 'P3'
$ SET PROCESS/PRIV=('P4')
$ 'P1'
$
