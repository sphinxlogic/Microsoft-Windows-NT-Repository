$ ! Procedure:	DBS_EDIT.COM
$ __vfy = "VFY_''f$parse(f$environment("procedure"),,,"name")'"
$ if (f$type('__vfy') .eqs. "") then __vfy = 0
$ __vfy_saved = f$verify(&__vfy)
$	set noon
$	say = "write sys$output"
$	option	    = P1
$	input_file  = P2
$	pindex = f$fao("!4XL" -
			,f$getsyi("maxprocesscnt")-f$getjpi("","proc_index"))
$	teco_output_killed = 0
$	work_file = "SYS$SCRATCH:TECO_''pindex'.WRK"
$	output_file = work_file
$	if (P3 .nes. "") then output_file = P3
$	output_file = f$parse(output_file,,,"device") -
				+ f$parse(output_file,,,"directory") -
				+ f$parse(output_file,,,"name") -
				+ f$parse(output_file,,,"type")
$	create_qual = "/nocreate"
$	if (option .eqs. "") then option = "TECO"
$	if (option .eqs. "MAKE") then create_qual = "/create"
$	gosub check_memory
$	edit_jnlfnd = "!/%DBS_EDIT-W-JNLFND, journal file !AS was found"
$	edit_check  = "-DBS_EDIT-W-CHECK, the file may be in use or a" -
			+ " previous edit aborted"
$	edit_check2 = "-DBS_EDIT-W-CHECK2, please check before continuing"
$	edit_abort  = "-DBS_EDIT-W-ABORT, this edit is being aborted...!/"
$	tjl_file = ""
$	jou_file = ""
$	if (f$parse(input_file,,,"name") .nes. "")
$		then tjl_file = f$parse(input_file,,,"device") -
				+ f$parse(input_file,,,"directory") -
				+ f$parse(input_file,,,"name")
$		jou_file = tjl_file + ".JOU"
$		tpu_file = tjl_file + ".TPU$JOURNAL"
$		tjl_file = tjl_file + ".TJL"
$		journal	 = jou_file
$		if (f$search(journal) .nes. "") then goto journal_found
$		journal	 = tpu_file
$		if (f$search(journal) .nes. "") then goto journal_found
$		journal	 = tjl_file
$		if (f$search(journal) .nes. "") then goto journal_found
$		open/write jou 'jou_file'
$		write jou "''f$getjpi("", "username")' ''f$time()'"
$		write jou "Input file <''input_file'>"
$		write jou "Work file  <''output_file'>"
$		close/nolog jou
$	endif !(f$parse(input_file,,,"name") .nes. "")
$	define/user/nolog sys$input sys$command
$	if (option .nes. "MAKE")
$		then editt/teco/command/memory'create_qual' -
			/output='output_file' 'input_file'
$	else
$	editt/teco/command/memory'create_qual'/output='output_file'
$	endif !(option .nes. "MAKE")
$	if (input_file .nes. "") then define/nolog tec$memory "''input_file'"
$	if (.not. teco_output_killed)
$		then gosub check_memory
$		if (input_file .nes. "")
$			then
$			if ((output_file .nes. input_file) -
				.and. (P3 .eqs. "") -
				.and. (f$search(output_file) .nes. ""))
$				then
$				say "Creating new version of ''input_file'"
$				copyy/nolog 'output_file' 'input_file'
$			endif
$		endif
$	endif
$	if (jou_file .nes. "") then deletee/nolog 'jou_file';
$ goto bail_out
$journal_found:
$	say f$fao(edit_jnlfnd, journal)
$	say f$fao(edit_check)
$	say f$fao(edit_check2)
$	say f$fao(edit_abort)
$	directoryy/nohead/notrail/owner/date=created 'journal'
$ goto bail_out
$bail_out:
$	if (f$search(work_file) .nes. "") then deletee/nolog 'work_file';*
$ !'f$verify(__vfy_saved)'
$ exit
$check_memory:
$	if (input_file .eqs. "") then -
$		if (f$trnlnm("TEC$MEMORY") .nes. "") then -
$			input_file = f$trnlnm("TEC$MEMORY")
$	if (input_file .nes. "") then -
$		input_file = f$parse(input_file,,,"device") -
				+ f$parse(input_file,,,"directory") -
				+ f$parse(input_file,,,"name") -
				+ f$parse(input_file,,,"type")
$ return
$ !+==========================================================================
$ !
$ ! Procedure:	DBS_EDIT.COM
$ !
$ ! Purpose:	A procedure to redirect teco's output to other than the input
$ !		filename (initially) and to create a journal file of sorts so
$ !		that other procedures can detect the editing session.
$ !		Once the edit is complete the output is then copied back to
$ !		where it should be.
$ !
$ ! Parameters:
$ !	 P1	Either TECO, MAKE or MUNG.  Anything else will result in the
$ !		equivalent of a TECO.
$ !	[P2]	The filename to edit, will default to TEC$MEMORY if not given.
$ !	[P3]	Optional output file, usefull for edit/copy jobs.
$ !
$ ! History:
$ !		24-Jan-1989, DBS; Version V1-001
$ !  	001 -	Original version.
$ !		09-Feb-1989, DBS; Version V1-002
$ !	002 -	Changed name of work file slightly to only use the last four
$ !		digits of the pid.
$ !		17-Feb-1989, DBS; Version V1-003
$ !	003 -	Added P3 processing to specify an output file (also MUNG is
$ !		not valid any more).
$ !		04-Sep-1989, DBS; Version V1-004
$ !	004 -	Changed pid to process index for work file name.
$ !		19-Sep-1989, DBS; Version V1-005
$ !	005 -	Modified to use the symbol now generated by VTEDIT called
$ !		teco_output_killed and cleaned up the handling of the workfile
$ !		stuff that sometimes got left hanging around.
$ !		10-Jul-1998, DBS; Version V1-006
$ !	006 -	Changed the "journal" file from .TJL to .JOU.
$ !-==========================================================================
