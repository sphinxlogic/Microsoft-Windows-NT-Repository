$ DEFDIR = F$ENVIRONMENT("DEFAULT")
$ COP := COPY/LOG
$ PUR := PURGE/LOG
$!
$! Get directory that install.com resides in.
$ MYPROC = F$ENVIRONMENT("PROCEDURE")
$ MYDEV = F$PARSE(MYPROC,,,"DEVICE","SYNTAX_ONLY")
$ MYDIR = F$PARSE(MYPROC,,,"DIRECTORY","SYNTAX_ONLY")
$ MYDIR = MYDEV + MYDIR
$!
$ ON ERROR THEN GOTO ERROR_EXIT
$!
$! Are we AXP or VAX?
$ ARCHITECTURE = F$GETSYI("ARCH_NAME")
$ IF ARCHITECTURE .EQS. "VAX"
$ THEN
$   EXE_DIR := [.VAX]
$ ELSE
$   EXE_DIR := [.AXP]
$ ENDIF
$!
$! Set def to directory where install.com resides.
$ SET DEF 'MYDIR'
$!
$! And now to the architecture sub directory
$ SET DEF 'EXE_DIR'
$!
$! Copy the files to their system directories.
$ COP UCX$SMTP_RECEIVER.EXE SYS$COMMON:[SYSEXE]
$ COP UCX$SMTP_SYMBIONT.EXE SYS$COMMON:[SYSEXE]
$ COP UCX$SMTP_*SHR*.EXE SYS$COMMON:[SYSLIB]
$ COP UCX$POP_SERVER.EXE SYS$COMMON:[SYSEXE]
$!
$! Stop and start SMTP and POP to get the new files installed.
$ WRITE SYS$OUTPUT "Stopping UCX SMTP"
$ @SYS$MANAGER:UCX$SMTP_SHUTDOWN.COM
$ WRITE SYS$OUTPUT "Stopping UCX POP"
$ @SYS$MANAGER:UCX$POP_SHUTDOWN.COM
$ WRITE SYS$OUTPUT "Restarting UCX SMTP"
$ @SYS$MANAGER:UCX$SMTP_STARTUP.COM
$ WRITE SYS$OUTPUT "Restarting UCX POP"
$ @SYS$MANAGER:UCX$POP_STARTUP.COM
$!
$! Purge old files if user says to
$ ASKPURGE:
$ INQUIRE RESP "Do you want to purge the old versions of the UCX SMTP and POP images from your system disk? "
$ RESP = F$EDIT(RESP, "UPCASE, TRIM")
$ CHAR = F$EXTRACT(0, 1, RESP)
$ IF CHAR .NES. "Y" .AND. CHAR .NES. "N" THEN GOTO ASKPURGE
$ DOPURGE:
$ IF CHAR .EQS. "Y"
$ THEN
$   PUR SYS$SYSTEM:UCX$POP_SERVER.EXE
$   PUR SYS$SYSTEM:UCX$SMTP_RECEIVER.EXE
$   PUR SYS$SYSTEM:UCX$SMTP_SYMBIONT.EXE
$   PUR SYS$LIBRARY:UCX$SMTP_MAILSHR.EXE
$   PUR SYS$LIBRARY:UCX$SMTP_PARSESHR*.EXE
$ ENDIF
$!
$ EXIT_PROC:
$ SET DEF 'DEFDIR'
$ EXIT
$!
$ ERROR_EXIT:
$ GOTO EXIT_PROC
