!------------------------------------------------------------------------------
!
!  CLD module for Pascal Pre-processor program.  Compile this module as
!  follows:  $ SET COMMAND /OBJECT PP_CLD
!
!  Author:  Jonathan Ridler.
!
!------------------------------------------------------------------------------

MODULE  PP_CLD

DEFINE  VERB  PP

	PARAMETER  P1, LABEL=PASCALSOURCE, PROMPT="Pascal Source File",
		    VALUE(REQUIRED,TYPE=$INFILE)

	QUALIFIER  ALL, VALUE (TYPE=RESERVED_SYMBOL_STYLE), NONNEGATABLE
	QUALIFIER  DOLLARS, DEFAULT, VALUE (TYPE=RESERVED_SYMBOL_STYLE)
	QUALIFIER  HELP, SYNTAX=HELP
	QUALIFIER  IDENTIFIERS, DEFAULT, VALUE (TYPE=IDENTIFIER_STYLE)
	QUALIFIER  LOG, DEFAULT
	QUALIFIER  OUTPUT, VALUE (TYPE=$OUTFILE,REQUIRED)
	QUALIFIER  RESERVED_SYMBOLS, DEFAULT, VALUE (TYPE=RESERVED_SYMBOL_STYLE)
	QUALIFIER  TAB, VALUE (LIST,TYPE=$NUMBER,DEFAULT=8)
	QUALIFIER  VERSION
	QUALIFIER  VOCABULARY, NONNEGATABLE, VALUE (REQUIRED,TYPE=$INFILE)
	QUALIFIER  WARNINGS, DEFAULT
	QUALIFIER  WIDE_SCREEN

	DISALLOW ALL AND (DOLLARS OR IDENTIFIERS OR RESERVED_SYMBOLS)

	DEFINE  TYPE  IDENTIFIER_STYLE
	  KEYWORD  FIRST_CHARS_UP, DEFAULT
	  KEYWORD  LOWERCASE
	  KEYWORD  NOCHANGE
	  KEYWORD  UPPERCASE

	DEFINE  TYPE  RESERVED_SYMBOL_STYLE
	  KEYWORD  FIRST_CHARS_UP
	  KEYWORD  LOWERCASE
	  KEYWORD  NOCHANGE
	  KEYWORD  UPPERCASE, DEFAULT


DEFINE  SYNTAX  HELP

	NOPARAMETERS
