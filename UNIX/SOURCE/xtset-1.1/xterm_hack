#
# This stuff causes the window title to be $cwd@hostname so you know where
# you are. Also sets the icon name to be hostname in case the wm wasn't
# set up right.
#
# Things are updated on a cd, pushd, popd, rlogin and telnet. Add other
# context changes as appropriate. This file must be sourced by csh, not
# merely execed.
#
# Added pad command for X25 remote logins.
#
# If running on a SysV machine then /usr/ucb should be in your path
# or hostname aliased to its real location.
#
# Now uses a separate program to do the setting via an alias for speed and
# flexibility. You can specify below the things youd like to appear on the
# icon and title.
#
# $Id: xterm_hack,v 1.10 93/04/27 13:29:33 msmith Exp $
#
# Martin Smith, msmith@lssec.bt.co.uk
#

# only do it if interactive and running xterm or XT override set

if ($?TERM == 1) then

if ($?XT == 1 || $TERM == xterm) then

if ($?XT_TITLE_STRING == 1) then
alias res_t "xtset -t $XT_TITLE_STRING"		# user supplied title string
else
alias res_t 'xtset -t %d@%h \(%u:%g-%m\) %e'	# reset titlebar
endif

if ($?XT_ICON_STRING == 1) then
alias res_i "xtset -i $XT_ICON_STRING"		# user supplied icon string
else
alias res_i 'xtset -i %h'			# reset icon name
endif

alias cd 'cd \!* ; res_t'
alias su 'set save = $XTXTRA ; setenv XTXTRA " su \!*@`date +%H:%M`" ; res_t; /bin/su \!* ; setenv XTXTRA $save ; unset save ; res_t'
alias pushd 'pushd \!* ; res_t'
alias popd 'popd ; res_t'
alias newgrp 'newgrp \!* ; res_t'
alias umask 'umask \!* ; res_t'

setenv XTXTRA 'Login@'`date +%H:%M`

res_t
res_i

alias rlogin  'rlogin \!* ; res_i ; res_t'
alias telnet  'telnet \!* ; res_i ; res_t'
alias pad     'pad \!* ; res_i ; res_t'

endif
endif

